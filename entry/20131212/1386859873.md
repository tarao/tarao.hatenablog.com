---
Title: 【はてなスタッフ非公式ブログバトン】 Haxeの善し悪し
Date: 2013-12-12T23:51:13+09:00
URL: http://tarao.hatenablog.com/entry/20131212/1386859873
EditURL: https://blog.hatena.ne.jp/tarao/tarao.hatenablog.com/atom/entry/6653586347149235883
---

id:hatz48:detailさんからバトンがまわってきました.

前々から個人的に興味があったので, 社内で言語の話題が出る度に「Haxe, Haxe」と言っていたら, 「Haxeと言えばtaraoさん」みたいになってて, なぜかHaxeについて書く羽目になってしまった. <strong>Haxeなんて1ミリも書いたことないのに!</strong> この記事は http://try.haxe.org/ でちょっと遊んだ程度のにわか知識で書かれております.

もともとはよさげなAltJSを紹介していくみたいな文脈のようだけど, 個人的には特殊用途以外では素のJavaScript書いてりゃいいんじゃね, と思うので, 文脈無視で単純に言語/処理系の善し悪しについて書くよ!

====
** Haxeってなに

http://haxe.org/ によれば「マルチプラットフォーム オープンソース プログラミング言語」らしい. AltJSなんてケチくさいこと言ってないで複数のターゲット言語に変換して実行できるようにしようぜ, という感じの言語.

** よいところ

*** ターゲットのプラットフォームが多い

JavaScriptはもちろん, Flashにしたり, C++に変換してiOSやAndroidで動かしたり, 仮想機械語に翻訳してサーバサイドで動かしたり, とにかく多用途. しかもプラットフォーム依存なコード, たとえばJavaScriptをターゲットにしてjQueryを使うというようなものも書ける. さらにそういうプラットフォーム依存なコードは条件コンパイルできるので複数プラットフォームで共有するコードも書ける.

サポートされているプラットフォームの完全なリストは https://github.com/HaxeFoundation/haxe にある.

*** 速いらしい

[http://togetter.com/li/315178:title=ちょっと前にJavaScriptに変換した場合の速度がJSX並]という話があったので, リッチなクライアントサイドアプリ案件にも使えそう. ターゲット言語がJavaScriptでなくてもよいならC++に変換すれば爆速だろうし, 速いのはよいことだ!

*** 構造的部分型

ふつうの型は型を名前で区別していて, ひとつひとつの型はユニークではあるけれど同列であって, 部分型(サブタイプ)を考える場合には言語に組込みで型間の順序が定義されている(たとえば<code>Int &lt;: Float</code>)か, プログラマが自分で(<code>extends</code>とかで)定義する. 自動的に部分型関係が定義されるということはない.

構造的部分型は, 型に単なる名前ではない構造をもったものも許容して, その構造から自明に決まる順序を使って部分型関係を定義する. つまりプログラマが<code>extends</code>みたいなものをいちいち書く必要がない.

次の例の場合, <code>Point3D</code>は自動的に<code>Point</code>の部分型になっている.

>|actionscript|
typedef Point = { x : Int, y : Int }
typedef Point3D = { x : Int, y : Int, z : Int }

function expectPoint(pt : Point) {
  ...
}

var pt3d : Point3D = { x : 0, y : 0, z : 0 };
expectPoint(pt3d); // OK!
||<

これはなんかもう見るからにJavaScriptで書いてたコードをHaxe化するときにやりやすそう. そもそもクラスによる部分型も構造的部分型で模倣できる((たとえば, クラス<code>C</code>は<code>$C</code>というダミーのフィールドを必ず持っていて, <code>C</code>を<code>extends</code>した<code>D</code>は<code>$C</code>と<code>$D</code>というフィールドを必ず持つようにする))から, なんだ, クラスなんて要らんかったんや! Haxeにはクラスもあるけど(もしかしたら内部的には構文糖衣かもしれない).

*** 直和型

<code>enum</code>で直和型が定義できる.

>|actionscript|
enum Color {
  Red;
  Green;
  Blue;
  Gray(v : Int);
  RGB(r : Int, g : Int, b : Int);
}
||<

使うときは<code>switch</code>で.

>|actionscript|
function toInt(c : Color) {
  return switch(c) {
    case Red: 0xFF0000;
    case Green: 0x00FF00;
    case Blue: 0x0000FF;
    case Gray(v): (v<<16) | (v<<8) | v;
    case RGB(r,g,b): (r<<16) | (g<<8) | b;
  };
}
||<

<code>switch</code>が式なのがよい! ちなみに, きちんと直和型なので網羅性もチェックされる. 場合分けを忘れるとコンパイルエラーになるので実行しなくてもミスが発見できる(実際これを書くときに怒られて気づいた).

>|actionscript|
function toInt(c : Color) {
  return switch(c) { // Unmatched patterns: Blue
    case Red: 0xFF0000;
    case Green: 0x00FF00;
    case Gray(v): (v<<16) | (v<<8) | v;
    case RGB(r,g,b): (r<<16) | (g<<8) | b;
  };
}
||<

さらに型引数をとってこんなものも書ける.

>|actionscript|
enum Cell<T> {
  Nil;
  Cons(item : T, next : Cell<T>);
}
||<

>|actionscript|
Cons(1, Cons(2, Cons(3, Nil)));
||<

もっと実用的なものとしてはオプション型とか定義できる. みんな大好きMaybeモナド!

>|actionscript|
enum Maybe<T> {
  Nothing;
  Just(v : T);
}
||<

>|actionscript|
var s1 : Maybe<String> = Just('foo');
var s2 : Maybe<String> = Nothing;
new js.JQuery("body").html(switch (s1) {
  case Nothing: 'nothing to print';
  case Just(s): s;
});
||<

これで<code>null</code>を撲滅できるね!

まぁクラスがあれば直和型はだいたい実現できる((参考: [http://d.hatena.ne.jp/tarao/20120711/1341969567:title]))のだけど, 言語によるサポートがあると構文が簡単で捗る.

*** 多相型

多相型のない型付言語なんてゴミだよね. Haxeでは記法はアレだけど多相型が使える.

>|actionscript|
function id<T>(x : T) {
  return x;
}

id(3);
id('foo');
||<

*** 漸進的型付け(gradual typing)

<code>Dynamic</code>型として宣言した変数は, どんな型を要求するところにも使えて, どんなプロパティにもアクセスできて, どんな型の値も入れられる. JavaScriptのコードをHaxe化したいという場合はとりあえずぜんぶ<code>Dynamic</code>型にしておいて, だんだん型を書いていくとよさそう. だんだん型をつけるので漸進的型付け(gradual typing)と呼ばれている.

>|actionscript|
var d : Dynamic = {};
d.x = 3;
d.y = 5;
var pt : { x : Int, y : Int } = d;
d = 'foo';
||<

[http://siek.blogspot.jp/2012/10/is-typescript-gradually-typed-part-2.html:title=Siekの分類]によれば漸進的型付けには3段階ある.

- レベル1: 型検査はするが実行に際しては型をただ取り去るだけ
- レベル2: <code>Dynamic</code>型と他の型との境界で, キャストできるか実行時に検査が行なわれる
- レベル3: 実行時のキャストはなるべく遅れて起きるが, 失敗時のエラーの原因として境界部分の箇所が追及される

レベル3はちょっと難しい話なので置いておくとして((詳しく知りたい人は[http://siek.blogspot.jp/2012/10/is-typescript-gradually-typed-part-2.html:title=Siekの解説のLevel 3のところ]か, Wadlerの[http://homepages.inf.ed.ac.uk/wadler/papers/blame/blame-tr.pdf:title=Well-typed programs can't be blamed]を読もう)), レベル2は, <code>Dynamic</code>型を使ったことでもしもエラーが起きたなら, それは<code>Dynamic</code>型の部分が原因, という一見当たり前に思える性質が保証されていることを意味する. けれどこれはぜんぜん当たり前ではなくて, たとえばTypeScriptではそういうことは保証されないし, HaxeでもJavaScriptに変換する場合は保証されない(つまりレベル1になっている). 確かめてみよう!

>|actionscript|
class Test {
  static function main(){
    var x : Dynamic = new Bar();
    new js.JQuery("body").html(test(x));
  }
  static function test(foo : Foo) {
    return foo.foo(); // TypeError: foo.foo is not a function
  }
}

class Foo {
  public function new(){}
  public function foo(){ return "foo"; }
}

class Bar {
  public function new(){}
  public function bar(){ return "bar"; }
}
||<

<code>Dynamic</code>型の<code>x</code>はどこにでも渡せるので, コンパイルは通る. 実行してみると, <code>foo.foo()</code>のところでエラーになる.

型を除去して実行したらこうなるのは当たり前だけど, これはJSだからよかったものの, たとえばC++に変換する場合で最適化も効かせるなら, ふつうに考えて<code>test</code>メソッドは<code>Foo</code>クラスの値しか受け取らないのだから, <code>foo.foo()</code>は静的に解決してしまおうとするはずで, そこで<code>foo</code>が実はべつのクラスでした, なんてことではもうsegmentation faultは避けられない. だから最適化したいということまで考えるなら(あるいは静的型付けって本来そういう最適化が可能なはずのものと思うなら), 当然<code>Dynamic</code>でない型しか出てこない部分では予期せぬ事態は絶対に起きないことを保証すべき. それがレベル2で言っていること.

JavaScript以外のプラットフォームへの変換もサポートしているHaxeは, この辺もちゃんと考えているようで, たとえばFlashをターゲットにした場合はどうやらレベル2になっていそう.

>|actionscript|
class Test {
  static function main(){
    var x : Dynamic = new Bar();
    try {
      trace('test()');
      test(x);
    } catch (err : Dynamic) { trace(err); }
  }
  static function test(foo : Foo) {
    trace('foo()');
    return foo.foo();
  }
}

class Foo {
  public function new(){}
  public function foo(){ return "foo"; }
}

class Bar {
  public function new(){}
  public function bar(){ return "bar"; }
}
||<
>||
Test.hx:5: test()
Test.hx:7: TypeError #1034
||<

なんかエラートレースを出す方法がよくわからなかったので<code>printf</code>デバッグ的になっていてわかりにくいけれど, <code>foo.foo()</code>が呼ばれる前, もっと言うと<code>test()</code>の本体に入る前でエラーになっている. <code>Dynamic</code>型の変数を<code>Foo</code>型が期待されるコンテキストに渡しているところが境界なので, レベル2なら<code>test()</code>の呼び出しのところでエラーになるはず(<code>"test()"</code>は印字されるけど<code>"foo()"</code>が印字される前に例外で脱出するはず)なので, 正しそう! やるじゃん.

まとめると, HaxeはJavaScriptをターゲットとした場合はレベル1の漸進的型付けでしかないけれど, プラットフォームによっては(すくなくとも)レベル2の漸進的型付けを実現していそう! すごい!

*** 型推論

静的型付言語ってなんども型書かないといけなくて面倒くさいだって? それいつの時代の話? 型推論できなくても許されるのは小学生までだよねー. Haxeはもちろん型推論してくれる. みんな大好き型推論!

>|actionscript|
var arr = [ 'foo', 'bar' ]; // arr : Array<String>

function f(x) {
  ...
}
var s = 'hoge' + f(3); // f : Int -> String
||<

** わるいところ

*** ターゲットのプラットフォームによって意味論がちがう

漸進的型付けのところで見た通り, どのプラットフォームをターゲットにするかによって言語の意味論が変わってしまう. これは言語仕様を曖昧にして, とくに静的型付言語に求められる型安全性の保証を難しくしてしまうのではないかという気がする.

*** 多相型が型推論されない

メソッドの型を推論したとき, 基本的に推論される型は単相なので, 多相的に使いたいメソッドは型を書かないといけない.

>|actionscript|
function id(x) {
  return x;
}
id(3); // id : Int -> Int
id('foo'); // コンパイルエラー "String should be Int"
||<

>|actionscript|
function id<T>(x : T) {
  return x;
}
id(3); // id : Unknown<0> -> Unknown<0>
id('foo'); // id : Unknown<0> -> Unknown<0>
||<

まぁ<code>var f = (function(){ return function(x){ ... }; })();</code>みたいに書いたときはパラメータ多相とかいろいろあるので単相な方が正しそうだけど, すくなくとも<code>function</code>宣言のときは多相になってもいいんじゃないか. OCamlとかに慣れてると正直つらい. <code>&lt;T&gt;</code>みたいなJavaやC++っぽいのは極力書きたくない.

*** 型推論と構造的部分型の組み合わせが微妙

なぜか以下のコードがコンパイルエラーになる.

>|actionscript|
var pt3d = { x : 1, y : 2, z : 3 }; // pt3d : { z : Int, y : Int, x : Int }
function f(pt : { x : Int, y : Int }) {
  ...
}
f(pt3d); // コンパイルエラー: "{ z : Int, y : Int, x : Int } has extra field z"
||<

<code>pt3d</code>には<code>{ z : Int, y : Int, x : Int }</code>という型が推論されているはずなのに, なぜか<code>{ x : Int , y : Int }</code>を要求する<code>f</code>に渡そうとすると怒られる. ちなみに, 以下のように書けば通る.

>|actionscript|
var pt3d : { x : Int, y : Int, z : Int } = { x : 1, y : 2, z : 3 }; // pt3d : { z : Int, y : Int, x : Int }
function f(pt : { x : Int, y : Int }) {
  ...
}
f(pt3d); // OK
||<

*** 型推論が止まらないかもしれない

構造的部分型(の暗黙的キャストを許す場合) + 再帰型 + 多相型の組み合わせで型推論/型検査が決定可能という話は聞いたことがない(ふつうに未解決問題なんじゃないか)ので, Haxeの型推論/型検査が必ず停止するかどうかはだいぶあやしい気がする. 少なくとも止まることが保証されているという話は見つけられなかった. まぁこういうのはよくあることで, 有名どころではたとえばJavaの型検査の決定可能性は未解決問題で, 実際いままでの型検査実装では無限ループするような入力例も見つかっている(([http://research.microsoft.com/apps/pubs/default.aspx?id=64041:title=Kennedy and Pierce. "On Decidability of Nominal Subtyping with Variance", FOOL/WOOD '07, 2007.])). もしかすると, この辺をad-hocに解決しようとした結果として型推論と構造的部分型の組み合わせが微妙になっているのかもしれない.

** まとめ

なんか着眼点が趣味まるだしだけどHaxeはまぁまぁいい言語だと思った. ぜんぜんベストではないけど.

** 参考

- [http://d.hatena.ne.jp/osiire/20091105:title]
- [http://d.hatena.ne.jp/mzp/20120613/jsx:title]

** これまでの記事

- [http://hitode909.hatenablog.com/entry/2013/12/10/140723:title]
- [http://hatz48.hatenablog.com/entry/2013/12/11/140550:title]

** 次回

次はid:aereal:detailさんです!

** 追記: 2013-12-13

*** よいところの書き忘れ

細かく解説するほど推す機能ではないけれど, [http://haxe.org/manual/macros:title=マクロがある]のも(個人的には)よい. なにもなくて自分でプリプロセスするくらいなら最初から用意されている方がいざというときのためにはよいと思う. マクロを使いまくるのがよいかどうかは別として.

*** ブックマークコメントへの返信

[http://b.hatena.ne.jp/entry/173080850/comment/paq:embed]

この記事を書く前には本当に全く書いたことがありませんでした. 数行のコード断片は見たことがありましたが, まとまった量のHaxeのコードは読んだこともありません. この記事を書きはじめるにあたって, http://try.haxe.org/ を1日触っただけです.

なんというか, Haxe自身の個々の言語パラダイムが特別新しいものではないと思える程度に複数の言語に触れたことがあれば, こんなものでは?

*** ブックマークコメントへの返信

[http://b.hatena.ne.jp/entry/173080850/comment/shohei909:embed]

書き方が雑だったので申し訳ないのですが, コンパイルエラーになるのが「わるいところ」だと言いたいのではなくて, 型推論が弱いことがデメリットになっているという趣旨です.

少なくとも今回指摘した例は, どちらも型推論によってより一般的な型が推論されればコンパイルエラーにならず, 型安全性を損うことなく実行できるものです. 実際に型推論に頼らずに自分で型を書けばコンパイルに成功するということを指摘しているのはそのためでした. 型推論が弱いために, 自分で型を書けばコンパイルに成功するものでも, 型推論に任せるとダメだと言われてしまうということです.

そしてこのように型推論を弱くしているのは, 強めようとすると型システムがリッチすぎて決定不能になってしまうからではないか, だとしたらリッチな型システムを採用するために型推論は妥協していることになり, それはデメリットと言えるのではないか, というのが趣旨です. もちろん型システムのリッチさについては「よいところ」として言及しているので, 全体としてはトレードオフだということが言いたいわけです.
